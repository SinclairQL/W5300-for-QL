         section parser
         xdef     read_ip_address
; On entry from driver:
; a1 points to driver name (with no length word at beginning, fixed length of 3 bytes)
; a2 points to string passed to driver in open call, e.g. "udp_127.0.0.1:80" preceded
; by a length word
match_udp_drv_name
         rts
; deleted...
isnum    moveq    #1,d0
         cmpi.b   #$30,d1
         blt.s    notnum
         cmpi.b   #$39,d1
         bgt.s    notnum
numok    moveq    #0,d0
notnum   rts
;
;
; a1 = pointer to start of string
; d4.w = length of string
rdnum    moveq    #0,d2                               ; Accumulator for the number
         moveq    #0,d3                               ; loop counter
rdnloop  cmp.w    d4,d3
         bge.s    rdldone
         move.b   0(a1,d3.w),d1
         bsr      isnum
         tst.b    d0
         bne.s    rdldone
         sub.b    #$30,d1
         ext.w    d1
;         mulu     #10,d2                 ; 42+2n (n=# 1's in ea) = 46 clocks + ea (8, imm) = 54 clocks, 4 reads from mem
         move.w   d2,d5                   ; 8 clocks, 2 reads
         asl.w    #3,d2                   ; 10+2n = 16 clocks, 2 reads
         add.w    d5,d2                   ; 8 clocks + ea (0, Dn direct), 2 reads
         add.w    d5,d2                   ; 8 clocks + ea (0, Dn direct), 2 reads
; end mulu #10,d2, total 40 clocks, 8 reads
         add.w    d1,d2
         addq.w   #1,d3
         bra      rdnloop
rdldone  tst.w    d3
         beq.s    no_number_read
         moveq    #0,d0
         rts
no_number_read
         moveq    #-1,d0
         rts
;
rdbyte   bsr      rdnum
         tst.b    d0
         bne.s    rdb_exit_err
         cmpi.w   #255,d2
         bgt.s    rdb_exit_err
         cmpi.w   #0,d2
         blt.s    rdb_exit_err
         moveq    #0,d0
         rts
rdb_exit_err
         moveq    #-1,d0
         rts
; Read an ip v4 address
; a1 = pointer to start of string
; d4.w = length of string
; 
; Returns
; d0 = success status, 0=ok
; d7 = ip address as long word
read_ip_address
         moveq    #2,d6                               ; Read the first three octets
read_ip_loop
         bsr.s    read_byte_and_update_regs
         tst.b    d0
         bne.s    not_ip
         cmpi.b   #'.',(a1)+
         bne.s    not_ip
         subq.w   #1,d4                               ; Update remaining length
         dbf      d6,read_ip_loop
         bsr.s    read_byte_and_update_regs
         tst.b    d0
         bne.s    not_ip
         cmpi.b   #0,(a1)+                            ; Last byte should be a terminating null
         bne.s    not_ip
         rts
not_ip   moveq    #-1,d0
         rts
read_byte_and_update_regs
         bsr      rdbyte
         tst.b    d0
         bne.s    abort_read
         asl.l    #8,d7
         move.b   d2,d7                               ; d2 is the accumulated byte value from 'rdbyte'
         adda.w   d3,a1                               ; d3 from 'rdbyte' shows how many chars were read
         sub.w    d3,d4
abort_read
         rts
; Read and resolve host name
; a1=pointer to start of string, read until ':'
; d4.w = length of string, updated as string is consumed
resolve_host_name
         movea.l  a1,a3
find_host_name
         move.b   (a3)+,d1
         cmpi.b   #':',d1
         beq.s    colon_after_host_name_found
         dbf      d4,find_host_name
         bra.s    invalid_host_name                   ; No colon after host name found -> invalid
colon_after_host_name_found
         suba.l   a1,a3
         move.w   a3,d2
         subq.w   #1,d2                               ; Length of string before ':', a3 is always incremented at least once
         cmp.w    #0,d2                               ; Was colon the first character? i.e. no host name given
         beq.s    invalid_host_name
; a1 still points to start of string, d2.w contains length of hostname string
;
; TODO call resolver here with the hostname
; update a1 to indicate host name has been consumed
		 adda.w   d2,a1
         moveq    #0,d0
         rts
invalid_host_name
         moveq    #1,d0
         rts
         end
